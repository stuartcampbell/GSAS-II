      SUBROUTINE REDUCE
      COMMON /CELLI/ AI,BI,CI,ALPI,BETI,GAMI,VOLI
      COMMON /CELLF/ AF,BF,CF,ALPF,BETF,GAMF,VOLF
      COMMON /DELTA1/ IRSS
      COMMON /DELTA4/ IDERIV,IFINIS,IQMATF
      COMMON /ERR1/ IERR1
      COMMON /PROB1/ NTD,IPROB
      COMMON /VAR1/ VAR90
C
C
C     SUBROUTINE 'REDUCE' ...
C        CONTROLS REDUCTION AND DERIVATIVE LATTICE CALCULATIONS
C
C
C
C     --- INITIALIZE VARIABLES
      IPROB = 1
C
  100 CONTINUE
C
C     --- BEGINNING OF LOOP TO PROCESS EACH INPUT CELL
C
C     --- WRITE PROBLEM NUMBER
      CALL OUTPT1(3)
C
C     --- INITIALIZE VARIABLES
      IDERIV = 0
      IQMATF = 0
      IFINIS = 0
C
C     --- READ AND INTERPRET RSS LINE
C         (RSS SPECIFICATIONS, CELL CENTERING, CELL PARAMETERS)
      CALL RD021
C
C     --- GO TO NEXT PROBLEM IF INPUT CELL HAS ILLEGAL CELL PARAMETER(S)
C         AND/OR CELL VOLUME (ERROR FLAG IERR1 IS SET IN *VOLUME*)
      IF(IERR1.EQ.1) GO TO 400
C
C        --- GENERATE UPPER TRIANGULAR MATRICES IF DERIVATIVE LATTICES
C            ARE TO BE CALCULATED
         IF(IRSS.NE.0) CALL QMATRI
C
  200    CONTINUE
C
C        --- STARTING POINT FOR REDUCTION CALCULATIONS
C
C        --- THIS STATEMENT DOES NOT APPLY TO REDUCTION-ONLY
C            CALCULATIONS ... GO TO NEXT THE PROBLEM IF ALL THE
C            DERIVATIVE LATTICES HAVE BEEN CALCULATED AND REDUCED
C            (IFINIS IS SET TO 1 IN DERIV AFTER THE LAST UPPER
C            TRIANGULAR MATRIX HAS BEEN SELECTED)
         IF(IFINIS.EQ.1) GO TO 400
C
C           --- FOR EACH INPUT CELL, REDUCE THE CELL AND, IF APPROPRIATE,
C               REDUCE ITS ASSOCIATED DERIVATIVE CELLS
            IF(IDERIV.GT.0) CALL DERIV
            CALL TRANS(0)
C
C           --- VAR90 DEFINES RANGE OF ANGLES TO BE CONSIDERED EQUAL
C               TO 90 DEGREES IN SUBROUTINE 'NORMAL'
            VAR90 = 0.000001
            CALL MNCOND
            VAR90 = 0.000349
            CALL NORMAL
            CALL SPCOND
            CALL TRANS(1)
C
C           --- IF DERIVATIVE LATTICES ARE NOT BEING CALCULATED,
C               GO TO NEXT PROBLEM
            IF(IRSS.EQ.0) GO TO 400
               IF(IDERIV.NE.0) GO TO 200
C
C              --- RESET INITIAL CELL TO BE THE REDUCED (PRIMITIVE) CELL
C                  OF THE LATTICE
               AI = AF
               BI = BF
               CI = CF
               ALPI = ALPF
               BETI = BETF
               GAMI = GAMF
               VOLI = VOLF
C
C              --- FIRST UPPER TRIANGULAR MATRIX WILL BE SELECTED
C                  TO CALCULATE DERIVATIVE LATTICES
               IDERIV = 1
               GO TO 200
C
  400 CONTINUE
C
C     --- FINISHED RSS CALCULATIONS FOR A SINGLE PROBLEM,
C         PROCEED TO NEXT PROBLEM IF REQUIRED
      IPROB = IPROB + 1
      IF(IPROB.LE.NTD) GO TO 100
      RETURN
       END
