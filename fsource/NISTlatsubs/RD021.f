      SUBROUTINE RD021
      CHARACTER AHP,AHA,AHB,AHC,AHF,AHI,AHH,AHR,ACNTR1,ACNTR2
      COMMON /CELLI/ AI,BI,CI,ALPI,BETI,GAMI,VOLI
      COMMON /CNTR1/ ICNTR1
      COMMON /DELTA1/ IRSS
      COMMON /DELTA2/ IDEL1,IDEL2
      COMMON /ERR1/ IERR1
      COMMON /UNIT1/ IUNITA
      COMMON /UNIT2/ IUNITB
      DATA AHP/'P'/, AHA/'A'/, AHB/'B'/, AHC/'C'/, AHF/'F'/, AHI/'I'/
      DATA AHR/'R'/, AHH/'H'/
C
C
C     SUBROUTINE 'RD021' ...
C          READS AND INTERPRETS RSS LINE
C
C
C
C     --- READ RSS LINE
C         (RSS SPECIFICATIONS, CELL CENTERING, CELL PARAMETERS)
      READ(IUNITA,6000) IRSS, IDEL1, IDEL2, ACNTR1, ACNTR2,
     $             AI, BI, CI, ALPI, BETI, GAMI
C
C     --- IF NOT REDUCTION ONLY, CHECK AND, IF NECESSARY, RESET
C         SPECIFICATIONS FOR DERIVATIVE LATTICE CALCULATIONS
      IF(IRSS.EQ.0) GO TO 100
         IF(IRSS.GT.3.OR.IRSS.LT.0) WRITE(IUNITB,6100)
         IF(IRSS.GT.3.OR.IRSS.LT.0) IRSS = 0
         IF(IDEL1.GT.IDEL2.OR.IDEL1.LT.2.OR.IDEL2.GT.9)
     $      WRITE(IUNITB,6200)
         IF(IDEL1.GT.IDEL2.OR.IDEL1.LT.2.OR.IDEL2.GT.9) IRSS = 0
  100 CONTINUE
C
C     --- WRITE HEADINGS FOR RSS SPECIFICATIONS
      CALL OUTPT1(4)
C
C     --- ASSIGN VARIABLE (ICNTR1) TO INDICATE CENTERING OF INPUT CELL
      ICNTR1 = 0
      IF(ACNTR1.EQ.AHP)  ICNTR1 = 1
      IF(ACNTR1.EQ.AHA)  ICNTR1 = 2
      IF(ACNTR1.EQ.AHB)  ICNTR1 = 3
      IF(ACNTR1.EQ.AHC)  ICNTR1 = 4
      IF(ACNTR1.EQ.AHF)  ICNTR1 = 5
      IF(ACNTR1.EQ.AHI)  ICNTR1 = 6
      IF(ACNTR1.EQ.AHR.AND.ACNTR2.EQ.AHH)   ICNTR1 = 7
      IF(ACNTR1.EQ.AHR.AND.ACNTR2.EQ.AHR)   ICNTR1 = 1
      IF(ICNTR1.EQ.0) WRITE(IUNITB,6300)
      IF(ICNTR1.EQ.0) ICNTR1 = 1
C
C     --- WRITE HEADINGS FOR CENTERING OF INPUT CELL
      CALL OUTPT1(5)
C
C     --- FIND MATRIX TO TRANSFORM THE INPUT CELL TO
C         A PRIMITIVE CELL OF THE LATTICE
      CALL CENTER
C
C     --- CALCULATE THE VOLUME OF THE INPUT CELL
C         (AND CHECK FOR VALID INPUT DATA)
      CALL VOLUME(AI,BI,CI,ALPI,BETI,GAMI,VOLI)
C
C     --- IF INPUT CELL HAS ILLEGAL CELL PARAMETER(S) AND/OR VOLUME,
C         WRITE ERROR MESSAGE (ERROR FLAG IERR1 IS SET IN *VOLUME*)
      IF(IERR1.EQ.1) WRITE(IUNITB,6400)
      RETURN
 6000 FORMAT(I1,2X,2I1,3X,2A1,6F10.2)
 6100 FORMAT(/1X,'*RD021* ERROR ... Invalid RSS specification.'
     $/1X,18X,'Only cell reduction calculations will be carried out.'//)
 6200 FORMAT(/1X,'*RD021* ERROR ... Invalid supercell/subcell range was
     $specified.'/1X,18X,
     $'Only cell reduction calculations will be carried out.'//)
 6300 FORMAT(/1X, '*RD021* WARNING ... Input centering symbol not legal
     $(i.e. P,A,B,C,I,F,RR,RH).',
     $/21X,'Program assumes that the input cell is primitive.')
 6400 FORMAT(/1X,'*RD021* ERROR ... Input cell has illegal cell paramete
     $r(s) and/or cell volume.'/)
      END
