      SUBROUTINE VOLUME(AX,BX,CX,ALPX,BETX,GAMX,VOLX)
      COMMON /CONST1/ RADIAN
      COMMON /ERR1/ IERR1
      COMMON /UNIT2/ IUNITB
C
C
C     SUBROUTINE 'VOLUME' ...
C          CALCULATE CELL VOLUME - THESE CALCULATIONS
C          ARE ALSO USED TO CHECK FOR VALID INPUT DATA
C
C
C
C     --- INITIALIZE VARIABLES
      VOLX = 0.0
      IERR1 = 0
C
C     --- WHEN ONE OR MORE CELL EDGES ARE <= 0.01,
C         SET ERROR FLAG AND DO NOT CALCULATE VOLUME
      IF(AX.GT.0.01.AND.BX.GT.0.01.AND.CX.GT.0.01) GO TO 100
         IERR1 = 1
         GO TO 400
  100 CONTINUE
C
C     --- WHEN ONE OR MORE CELL ANGLES ARE <= 0.1 OR >=180.0,
C         SET ERROR FLAG AND DO NOT CALCULATE VOLUME
      IF(ALPX.GT.0.1.AND.ALPX.LT.180.0.AND.BETX.GT.0.1.AND.BETX.LT.
     $   180.0.AND.GAMX.GT.0.1.AND.GAMX.LT.180.0) GO TO 200
         IERR1 = 1
         GO TO 400
  200 CONTINUE
C
C      --- CALCULATE THE COSINES OF THE CELL ANGLES
      COS1 = COS(ALPX/RADIAN)
      COS2 = COS(BETX/RADIAN)
      COS3 = COS(GAMX/RADIAN)
C
C     --- IF THE INTERMEDIATE EXPRESSION (TEM) IS GREATER THAN ZERO,
C         CALCULATE THE CELL VOLUME ... OTHERWISE, SET ERROR FLAG
C         SINCE DO NOT HAVE A LEGAL CELL
      TEM  = (1.0 - COS1**2 - COS2**2 - COS3**2 + 2.0*COS1*COS2*COS3)
      IF(TEM.GT.0.0) GO TO 300
         IERR1 = 1
         GO TO 400
  300 CONTINUE
      VOLX = AX*BX*CX*SQRT(TEM)
C
C     --- WRITE WARNING MESSAGE IF CALCULATED CELL VOLUME IS
C         EITHER VERY SMALL OR VERY LARGE
      IF(VOLX.LT.8.0.OR.VOLX.GT.50000.0) WRITE(IUNITB,6000)
  400 CONTINUE
      RETURN
 6000 FORMAT(/1X,'*VOLUME* WARNING ... Unusual cell volume has been calc
     $ulated.'/)
      END
